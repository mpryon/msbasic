.segment "CODE"
.ifdef EATER
T1CL   = $6004
T1CH   = $6005
ACR    = $600B
IER    = $600E

BEEP:
  jsr FRMEVL
  jsr MKINT

  ; Check if parameter is zero
  lda FAC+4
  ora FAC+3
  beq @silent

  ; Set T1 timer with parameter
  lda FAC+4
  sta T1CL
  sta SND1+1
  lda FAC+3
  sta T1CH
  sta SND1

  ; Start square wave on PB7
  lda #$c0
  sta ACR
  sta IER

  ; Get second parameter and store
  jsr CHKCOM
  jsr FRMEVL
  jsr MKINT
  lda FAC+4
  sta SND2+1
  lda FAC+3
  sta SND2

@silent:
  jsr CHKCOM
  jsr GETBYT
  cpx #0
  beq @done

@delay1:
  ldy #$ff
@delay2:
  nop
  nop
  dey
  bne @delay2
  dex
  bne @delay1

  ; Stop square wave on PB7
  lda #0
  sta ACR
  lda #$40
  sta IER

@done:
  rts

.endif
